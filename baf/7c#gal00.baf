IF
	!InParty("Galileo1")
	OR(2)
		Global("7C#GalileoInParty","GLOBAL",2)
		Global("7C#GalileoInParty","GLOBAL",0)
	OR(9)
		AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		SpellCastOnMe([GOODCUTOFF],WIZARD_BLINDNESS)
		SpellCastOnMe([GOODCUTOFF],WIZARD_FEEBLEMIND)
		SpellCastOnMe([GOODCUTOFF],WIZARD_RAY_OF_ENFEEBLEMENT)
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Enemy()
		DisplayStringHead(Myself,@7733) // Oi! What the bloody hell is your problem!
		SetGlobal("7C#GalileoInParty","GLOBAL",4)
		Continue()
END

IF
	Allegiance(Myself,ENEMY)
	!InParty(Myself)
	OR(2)
		Global("7C#GalileoInParty","GLOBAL",2)
		Global("7C#GalileoInParty","GLOBAL",0)
	!GlobalTimerNotExpired("7C#SpellCast","LOCALS")
	CheckStatLT(Myself,50,SPELLFAILUREMAGE)
	!StateCheck(Myself,STATE_MIRRORIMAGE)
THEN
	RESPONSE #100
		SetGlobal("7C#GalileoInParty","GLOBAL",5)
		SetGlobalTimer("7C#SpellCast","LOCALS",ONE_ROUND)
		ForceSpell(Myself,WIZARD_MIRROR_IMAGE)
END
	
IF
	!GlobalTimerNotExpired("7C#SpellCast","LOCALS")
	Global("7C#GalileoInParty","GLOBAL",5)
	Allegiance(Myself,ENEMY)
	See([PC])
	!StateCheck([PC],STATE_NOT_VISIBLE)
	CheckStatLT(Myself,50,SPELLFAILUREMAGE)
	HaveSpell(WIZARD_BLINDNESS)
	!StateCheck([PC],STATE_BLIND)
THEN
	RESPONSE #100
		SetGlobalTimer("7C#SpellCast","LOCALS",ONE_ROUND)
		Spell([PC],WIZARD_BLINDNESS)
END

IF
	!GlobalTimerNotExpired("7C#SpellCast","LOCALS")
	Global("7C#GalileoInParty","GLOBAL",5)
	Allegiance(Myself,ENEMY)
	See([PC])
	!StateCheck([PC],STATE_NOT_VISIBLE)
	CheckStatLT(Myself,50,SPELLFAILUREMAGE)
	HaveSpell(WIZARD_MAGIC_MISSILE)
	CheckStatLT([PC],50,RESISTMAGIC)
	CheckStatLT([PC],50,MAGICDAMAGERESISTANCE)
THEN
	RESPONSE #100
		SetGlobalTimer("7C#SpellCast","LOCALS",ONE_ROUND)
		Spell([PC],WIZARD_MAGIC_MISSILE)
END

IF
	!GlobalTimerNotExpired("7C#SpellCast","LOCALS")
	Global("7C#GalileoInParty","GLOBAL",5)
	Allegiance(Myself,ENEMY)
	See(Player1)
	!StateCheck(Player1,STATE_NOT_VISIBLE)
	CheckStatLT(Myself,50,SPELLFAILUREMAGE)
	HaveSpell(WIZARD_GLITTERDUST)
	NumInPartyGT(2)
	Range(Player1,20)
	OR(3)
		Range(Player2,30)
		Range(Player5,30)
		NumInPartyLT(2)
	OR(3)
		Range(Player3,30)
		Range(Player4,30)
		NumInPartyLT(3)
THEN
	RESPONSE #100
		SetGlobalTimer("7C#SpellCast","LOCALS",ONE_ROUND)
		Spell(Player1,WIZARD_GLITTERDUST)
END

IF
	!GlobalTimerNotExpired("7C#SpellCast","LOCALS")
	Global("7C#GalileoInParty","GLOBAL",5)
	Allegiance(Myself,ENEMY)
	See([PC])
	!StateCheck(Myself,STATE_NOT_VISIBLE)
	HaveSpell(WIZARD_INVISIBILITY)
	CheckStatLT(Myself,50,SPELLFAILUREMAGE)
THEN
	RESPONSE #100
		SetGlobalTimer("7C#SpellCast","LOCALS",ONE_ROUND)
		Spell(Myself,WIZARD_INVISIBILITY)
END

IF
	Global("7C#GalileoInParty","GLOBAL",5)
	Allegiance(Myself,ENEMY)
	See([PC])
	Range([PC],5)
	CheckStatLT([PC],50,RESISTSLASHING)
	StateCheck(Myself,STATE_MIRRORIMAGE)
	!HPPercentLT(Myself,65)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackReevaluate([PC],TWO_ROUNDS)
END