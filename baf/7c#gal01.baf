IF
	%bgt_check%
	!Global("7C#DoneFirewineRuins","GLOBAL",1)
	Dead("DICK")
THEN
	RESPONSE #100
		SetGlobal("7C#DoneFirewineRuins","GLOBAL",1)
END

IF
	%bgt_check%
	InParty("Galileo1")
	Global("7C#GalileoMelicampComment","LOCALS",0)
	PartyHasItem(%melicamp%)
THEN
	RESPONSE #100
		SetGlobalTimer("7C#GalileoTalkPlayer1","LOCALS",TWO_ROUNDS) // per Quartz's request
		SetGlobal("7C#GalileoMelicampComment","LOCALS",1)
END

IF
	%bgt_check%
	InParty("Galileo1")
	Global("7C#GalileoMelicampComment","LOCALS",1)
	!GlobalTimerNotExpired("7C#GalileoTalkPlayer1","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("7C#GalileoMelicampComment","LOCALS",2)
		Dialog("Player1")
END

IF
	%bgt_check%
	InParty("Galileo1")
	Detect("DeadHalflingMessager")
	Range("DeadHalflingMessager",20)
	Dead("DeadHalflingMessager")
	Global("7C#GalileoDeadHalflingComment","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("7C#GalileoDeadHalflingComment","LOCALS",1)
		Dialog("Player1")
END

IF
	%bgt_check%
	!InParty("Galileo1")
	OR(2)
		Global("7C#GalileoInParty","LOCALS",2)
		!Global("7C#GalileoInParty","LOCALS",1)
	!Allegiance(Myself,ENEMY)
	Global("KickedOut","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("KickedOut","LOCALS",1)
		SetGlobal("7C#GalileoInParty","LOCALS",1)
		Dialog("Player1")
END