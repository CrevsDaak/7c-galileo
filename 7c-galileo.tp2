BACKUP "7c-galileo/backup"
AUTHOR "Quartz and Crevs Daak"
VERSION "v0.1 alpha 18.12.14"

ALWAYS
  ACTION_IF "%LANGUAGE%" STR_CMP "english" BEGIN
    LAF HANDLE_CHARSETS
      STR_VAR
        tra_path = "7c-galileo/lang"
    END
  END

  OUTER_SPRINT tra "7c-galileo/lang/%LANGUAGE%"
  LOAD_TRA "%tra%/game.tra"
END

LANGUAGE
 "English"
 "english"
 "7c-galileo/lang/english/setup.tra"

BEGIN @1 DESIGNATED 100
REQUIRE_PREDICATE GAME_IS "bgee bgt eet tutu tutu_totsc" @2

ACTION_IF GAME_IS "tutu tutu_totsc" BEGIN
  OUTER_SPRINT bgt_check ""
  OUTER_SPRINT tutu_prefix "_"
  OUTER_SPRINT melicamp ~"_misc49"~
  OUTER_SPRINT dead_halfling _halfmir
  OUTER_SPRINT Gullykin fw4000
END ELSE ACTION_IF GAME_IS bgt BEGIN
  OUTER_SPRINT tutu_prefix ""
  OUTER_SPRINT melicamp ~"misc49"~
  OUTER_SPRINT dead_halfling halfmiri
  OUTER_SPRINT Gullykin ar9900
  OUTER_SPRINT bgt_check ~!Global("endofbg1","GLOBAL",2)~
END ELSE ACTION_IF GAME_IS bgee BEGIN
  OUTER_SPRINT tutu_prefix ""
  OUTER_SPRINT melicamp ~"misc49"~
  OUTER_SPRINT bgt_check ""
  OUTER_SPRINT dead_halfling halfmiri
  OUTER_SPRINT Gullykin ar4000
END

APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~0x80101FEF CD_STATE_NOTVALID~

ACTION_DEFINE_ASSOCIATIVE_ARRAY append_gtimes BEGIN
  ONE_ROUND => 6
  TWO_ROUNDS => 12
  THREE_ROUNDS => 18
  FOUR_ROUNDS => 24
  FIVE_ROUNDS => 30
  SIX_ROUNDS => 36
  SEVEN_ROUNDS => 42
  EIGHT_ROUNDS => 48
  NINE_ROUNDS => 54
  TEN_ROUNDS => 60
END

ACTION_PHP_EACH append_gtimes AS name => value BEGIN
  APPEND gtimes.ids ~%value% %name%~
    UNLESS ~%value% %name%~
END

COPY "7c-galileo/cre/galileo.cre" "override/7c#gal02.cre"
  SAY NAME1 @771
  SAY NAME2 @771
  SAY LEADER @772
  SAY BATTLE_CRY1 @773
  SAY BATTLE_CRY2 @774
  SAY BATTLE_CRY3 @775
  SAY BORED @776
  SAY TIRED @777
  SAY MORALE @778
  SAY SELECT_COMMON1 @779
  SAY SELECT_COMMON2 @7710
  SAY SELECT_COMMON3 @7711
  SAY SELECT_ACTION1 @7712
  SAY SELECT_ACTION2 @7713
  SAY SELECT_ACTION3 @7714
  SAY SELECT_RARE1 @7715
  SAY SELECT_RARE2 @7716
  SAY SPECIAL1 @7717
  SAY SPECIAL2 @7718
  SAY AREA_DAY @7719
  SAY AREA_NIGHT @7720
  SAY AREA_CITY @7721
  SAY CRITICAL_MISS @7728
  SAY CRITICAL_HIT @7729
  SAY PICKED_POCKET @7730
  SAY SET_A_TRAP @7731
  SAY SPELL_DISRUPTED @7732
  WRITE_ASCII 0x34 "7c#gas" #8
  WRITE_ASCII 0x3c "7c#gam" #8
  WRITE_ASCII 0x2cc "7c#gal01" #8
  WRITE_LONG 0x1c 57
  WRITE_BYTE 0x6a 30
  WRITE_ASCII 0x280 "Galileo1" #32
  WRITE_ASCII 0x248 "7c#gal01" #8
  ADD_KNOWN_SPELL SPWI106 #0 wizard
  ADD_KNOWN_SPELL SPWI206 #1 wizard
  ADD_MEMORIZED_SPELL SPWI106 #0 wizard #1
  ADD_CRE_ITEM "%tutu_prefix%ax1h01" #0 #0 #0 none weapon1 EQUIP
  ADD_CRE_ITEM "%tutu_prefix%dart01" #17 #0 #0 none weapon2
  ADD_CRE_ITEM "%tutu_prefix%dart01" #21 #0 #0 none inv1
  ADD_CRE_ITEM "%tutu_prefix%dart01" #18 #0 #0 none inv2
  PATCH_IF !GAME_IS bgee BEGIN
    ADD_CRE_ITEM "%tutu_prefix%shld09" #0 #0 #0 none shield
  END ELSE BEGIN
    ADD_CRE_ITEM shld10a #0 #0 #0 none shield
  END

COPY_EXISTING 7c#gal02.cre "override/7c#gal04.cre"
  WRITE_LONG 0x1c 73
  WRITE_BYTE 0x6a 40
  ADD_KNOWN_SPELL SPWI112 #0 wizard
  ADD_KNOWN_SPELL SPWI207 #1 wizard
  READ_LONG 0x2b0 mem_offset
  READ_LONG 0x2b4 mem_count
  SET found = 0
  SET extra_plus = 0x0
  SET extra_offset = 0x0
  FOR (i = 6; i < mem_count && found = 0 ; ++i) BEGIN
    SET extra_offset += extra_plus
    SET extra = 0x14 * extra_offset
    READ_SHORT (mem_offset + i * 0x10 + extra) spl_lvl
    READ_SHORT (mem_offset + 0x6 + i * 0x10 + extra) spl_type
    PATCH_IF spl_lvl = 0 && spl_type = 1 BEGIN
      WRITE_LONG (mem_offset + 0x2 + i * 0x10 + extra) 2
      WRITE_LONG (mem_offset + 0x4 + i * 0x10 + extra) 2
      READ_SHORT (mem_offset + 0xC + i * 0x10 + extra) extra_plus
    END
    PATCH_IF spl_lvl = 1 && spl_type = 1 BEGIN
      WRITE_LONG (mem_offset + 0x2 + i * 0x10 + extra) 1
      WRITE_LONG (mem_offset + 0x4 + i * 0x10 + extra) 1
    END
  END
  ADD_MEMORIZED_SPELL SPWI206 #1 wizard #1
  ADD_MEMORIZED_SPELL SPWI112 #0 wizard #1
  ADD_CRE_ITEM bull01 #34 #0 #0 none quiver1
  ADD_CRE_ITEM bull01 #26 #0 #0 none quiver2
  ADD_CRE_ITEM slng01 #0 #0 #0 none inv3
  LPF ADD_CRE_EFFECT
    INT_VAR
	opcode = 233
	parameter1 = 1
	parameter2 = 107
	timing = 9
  END

COPY_EXISTING 7c#gal04.cre "override/7c#gal06.cre"
  WRITE_LONG 0x1c 86
  WRITE_BYTE 0x6a 50
  READ_BYTE 0x53 save_death
  READ_BYTE 0x54 save_wands
  READ_BYTE 0x55 save_poly
  READ_BYTE 0x56 save_breath
  READ_BYTE 0x57 save_spell
  WRITE_BYTE 0x53 (save_death - 1)
  WRITE_BYTE 0x54 (save_wands - 2)
  WRITE_BYTE 0x55 (save_poly - 1)
  WRITE_BYTE 0x56 (save_breath - 1)
  WRITE_BYTE 0x57 (save_spell - 2)
  ADD_KNOWN_SPELL SPWI118 #0 wizard
  ADD_KNOWN_SPELL SPWI224 #1 wizard
  READ_LONG 0x2b0 mem_offset
  READ_LONG 0x2b4 mem_count
  SET found = 0
  SET extra_plus = 0x0
  SET extra_offset = 0x0
  FOR (i = 6; i < mem_count && found = 0; ++i) BEGIN
    SET extra_offset += extra_plus
    SET extra = 0x14 * extra_offset
    READ_SHORT (mem_offset + i * 0x10 + extra) spl_lvl
    READ_SHORT (mem_offset + 0x6 + i * 0x10 + extra) spl_type
    PATCH_IF spl_lvl = 0 && spl_type = 1 BEGIN
      WRITE_LONG (mem_offset + 0x2 + i * 0x10 + extra) 3
      WRITE_LONG (mem_offset + 0x4 + i * 0x10 + extra) 3
      READ_SHORT (mem_offset + 0xC + i * 0x10 + extra) extra_plus
    END
    PATCH_IF spl_lvl = 1 && spl_type = 1 BEGIN
      WRITE_LONG (mem_offset + 0x2 + i * 0x10 + extra) 2
      WRITE_LONG (mem_offset + 0x4 + i * 0x10 + extra) 2
    END
  END
  ADD_MEMORIZED_SPELL SPWI224 #1 wizard #1
  ADD_MEMORIZED_SPELL SPWI112 #0 wizard #1

APPEND pdialog.2da "GALILEO1            7C#GALP           7C#GALJ           7C#GALD"
  UNLESS "GALILEO1"

COMPILE EVAL "7c-galileo/d/before_joining.d" USING "%tra%/dialog.tra"
COMPILE EVAL "7c-galileo/d/party_dialog.d" USING "%tra%/dialog.tra"
COMPILE EVAL "7c-galileo/baf/7c#gal01.baf"

ACTION_IF GAME_IS "bgee eet" BEGIN
  COPY "7c-galileo/bmp/ee" override
END ELSE BEGIN
  COPY "7c-galileo/bmp/bg" override
END

COPY_EXISTING "%Gullykin%.are" override
  PATCH_IF BUFFER_LENGTH > 0x139 BEGIN
    READ_ASCII 0x94 are_script
    READ_LONG 0x54 actors_offset
    READ_SHORT 0x58 actors_count
    SET found = 0
    FOR (j = 0; j < actors_count && found > 1; ++j) BEGIN
      READ_SHORT 0x20 current_x
      READ_SHORT 0x22 current_y
      PATCH_IF (current_x > 1350 && current_x < 1450 && current_y > 600 && current_y < 700) BEGIN
        WRITE_SHORT 0x20 1044 
        WRITE_SHORT 0x22 694
        WRITE_SHORT 0x24 1044
        WRITE_SHORT 0x26 694
        SET found = 1
      END
    END
  END ELSE BEGIN
    PATCH_FAIL @3
  END
BUT_ONLY

ACTION_IF "%are_script%" STR_CMP "" BEGIN
  EXTEND_BOTTOM "%are_script%.bcs" "7c-galileo/baf/ar4000.baf" EVAL
END ELSE BEGIN
  COPY_EXISTING "%Gullykin%.are" override
    WRITE_ASCIIE 0x94 "%Gullykin%"
  BUT_ONLY

  EXTEND_BOTTOM "%Gullykin%.bcs" "7c-galileo/baf/ar4000.baf" EVAL
END

COPY_EXISTING "%tutu_prefix%gandol.dlg" override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~GiveGoldForce\(.*.$)\~ ~GiveGoldForce\1
SetGlobal("7C#DoneFirewineRuins","GLOBAL",1)~
  END
  UNLESS "7C#DoneFirewineRuins"
BUT_ONLY

COPY_EXISTING "%dead_halfling%.cre" override
  WRITE_ASCII 0x280 "DeadHalflingMessager" #32
BUT_ONLY

// eof
