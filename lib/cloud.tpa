/*
 * cloud.tpa
 *
 * This library needs the following files to
 * work:
 *   - 7c#cloud.2da
 *   - 7c#cloud.baf
 *   - 7c#cloud.cre
 *   - 7c#cloud.eff
 *   - 7c#sp318.baf
 *
 * Rudimentary implementation (read: shameless copy)
 * of SCS's method of detecting clouds, much simpler,
 * and doesn't rely on SFO.
 *
 */

ACTION_IF !FILE_EXISTS "override/7c#cloud.mrk" BEGIN

  COPY - "%cl%/7c#cloud.2da" "%cl%/7c#cloud.2da"
    READ_2DA_ENTRIES_NOW __7c#cloud__ 6
    FOR (in = 0; in < __7c#cloud__; ++in) BEGIN
      READ_2DA_ENTRY_FORMER __7c#cloud__ in 0 new
      READ_2DA_ENTRY_FORMER __7c#cloud__ in 1 res
      READ_2DA_ENTRY_FORMER __7c#cloud__ in 2 time
      READ_2DA_ENTRY_FORMER __7c#cloud__ in 3 shout
      READ_2DA_ENTRY_FORMER __7c#cloud__ in 4 type
      READ_2DA_ENTRY_FORMER __7c#cloud__ in 5 is_cloud
      INNER_ACTION BEGIN
        ACTION_IF FILE_EXISTS_IN_GAME "%res%.%type%" BEGIN
          COPY "%cl%/7c#cloud.baf" "%cl%/%new%.baf"
          COMPILE EVAL "%cl%/%new%.baf"

          COPY "%cl%/7c#cloud.cre" "override/%new%.cre"
            WRITE_ASCIIE 0x248 "%new%" #8

          COPY "%cl%/7c#cloud.eff" "override/%new%.eff"
            WRITE_ASCIIE 0x30 "%new%"

          ACTION_IF "%type%" STR_EQ "spl" BEGIN
            COPY_EXISTING "%res%.spl" override
              LPF ADD_SPELL_EFFECT
                INT_VAR
                  opcode = 177
                  target = 1
                  power = 10
                  parameter2 = 3
                STR_VAR
                  resource = EVAL "%new%"
              END
            BUT_ONLY
          END ELSE BEGIN
            COPY_EXISTING "%res%.itm" override
              LPF ADD_ITEM_EFFECT
                INT_VAR
                  opcode = 177
                  target = 1
                  power = 10
                  parameter2 = 3
                STR_VAR
                  resource = EVAL "%new%"
              END
            BUT_ONLY
          END
        END
      END
    END
  BUT_ONLY

  COMPILE EVAL "%cl%/7c#sp318.baf"

  COPY_EXISTING sw1h01.itm "override/7c#cloud.mrk"
END

// eof