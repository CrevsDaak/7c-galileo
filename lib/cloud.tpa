OUTER_SPRINT cl "7c-galileo/clouds"
OUTER_SET CrevsDaak 0x4461616b

/*
 * cloud.tpa
 *
 * WeiDU library by Nicolas Clotta (CrevsDaak)
 * Last edited on 2015-06-18.
 *
 * This library needs the following files to
 * work:
 *   - 7c#cloud.2da *   - 7c#cloud.baf *   - 7c#cloud.cre *   - 7c#cloud.eff *   - 7c#sp318.baf
 * 
 * It creates an invisible creature that Shout()s
 * to other creatures, whom move out away form
 * that creature right after hearing it, so they
 * get out of the cloud.
 *
 * The idea was taken from SCS's way of detecting
 * Cloudkill-like spells with stationary effects.
 *
 * Edited on purpose for the Galileo NPC mod. Use
 * the version alongside 7c-improved (ask Crevs).
 *
 */

OUTER_SPRINT 7c_det_cl_ver v1.1

ACTION_IF !FILE_EXSISTS "override/7c#cloud.mrk" BEGIN

  COPY - "%cl%/7c#cloud.2da" "%cl%/7c#cloud.2da"
    READ_2DA_ENTRIES_NOW __7c#cloud__ 6
    FOR (in = 0; in < __7c#cloud__; ++in) BEGIN
      READ_2DA_ENTRY_FORMER __7c#cloud__ in 1 new
      READ_2DA_ENTRY_FORMER __7c#cloud__ in 2 res
      READ_2DA_ENTRY_FORMER __7c#cloud__ in 3 time
      READ_2DA_ENTRY_FORMER __7c#cloud__ in 4 shout
      READ_2DA_ENTRY_FORMER __7c#cloud__ in 5 type
      READ_2DA_ENTRY_FORMER __7c#cloud__ in 6 is_cloud
      INNER_ACTION BEGIN
        ACTION_IF FILE_EXSISTS_IN_GAME "%res%.%type%" BEGIN
          COPY "%cl%/7c#cloud.baf" "%cl%/%new%.baf"
          COMPILE EVAL "%cl%/%new%.baf"

          COPY "%cl%/7c#cloud.cre" "override/%new%.cre"
            WRITE_ASCIIE 0x248 "%new%" #8

          COPY "%cl%/7c#cloud.eff" "override/%new%.eff"
            WRITE_ASCIIE 0x30 "%new%"
            WRITE_LONG 0x50 CrevsDaak

          ACTION_IF "%type%" STR_EQ "spl" BEGIN
            COPY_EXISTING "%res%.spl" override
              LPF ADD_SPELL_EFFECT
                INT_VAR
                  opcode = 177
                  target = 1
                  power = 10
                  parameter2 = 3
                STR_VAR
                  resource = EVAL "%new%"
              END
            BUT_ONLY
          END ELSE BEGIN
            COPY_EXISTING "%res%.itm" override
              LPF ADD_ITEM_EFFECT
                INT_VAR
                  opcode = 177
                  target = 1
                  power = 10
                  parameter2 = 3
                STR_VAR
                  resource = EVAL "%new%"
              END
            BUT_ONLY
          END
        END
      END
    END
  BUT_ONLY

  DELETE + "override/7c#sp318.bcs"
  COMPILE EVAL "%cl%/7c#sp318.baf"

  COPY_EXISTING sw1h01.itm "override/7c#cloud.mrk"
END

// eof