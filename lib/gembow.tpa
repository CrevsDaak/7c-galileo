// -*- weidu-tp2 -*-

// currently, it's a BG1 only file, since misc1j cannot be found in BG2 (misc6z has to be used instead).

COPY_EXISTING "%avaricem%.cre" override
  ADD_CRE_ITEM misc1j #0 #0 #0 identified INV
BUT_ONLY

ACTION_DEFINE_ASSOCIATIVE_ARRAY arrows BEGIN
  // empty on purpose
END

COPY_EXISTING_REGEXP - ~^.*\.itm$~ override
  PATCH_IF SOURCE_SIZE > 0x71 BEGIN
    PATCH_IF SHORT_AT 0x1c == 5 && LONG_AT 0x60 == 0 && (NOT "%SOURCE_RES%" STR_EQ "%tutu_var%AROW01") BEGIN
      $arrows("%SOURCE_RES%") = 1
    END
  END ELSE BEGIN
    PRINT ~ERROR: %SOURCE_FILE% is under 0x72 bytes in length: corrupt .itm file!~
  END
BUT_ONLY

ACTION_IF MOD_IS_INSTALLED stratagems.tp2 3010 BEGIN
  $arrows("%tutu_var%AROW02") = 0
END

COPY "7c-galileo/itm/gembow.itm" "override/c7gembow.itm"
  SAY 0x8 #6864
  SAY 0xC @7738
  SAY 0x50 #6866
  SAY 0x54 @7739
  WRITE_BYTE 0x19 (THIS | BIT3)
  PHP_EACH arrows AS arrow => n BEGIN
    PATCH_IF n == 1 BEGIN
      LPF ADD_ITEM_EFFECT
        INT_VAR
          parameter1 = RESOLVE_STR_REF (@7737)
          opcode = 180
          timing = 2
        STR_VAR
          resource = EVAL "%arrow%"
      END
    END
  END

COMPILE "7c-galileo/d/c7gembow.d" USING "7c-galileo/lang/%LANGUAGE%/gembow_dialog.tra"

ACTION_DEFINE_ASSOCIATIVE_ARRAY gembow BEGIN
  "MISC19" => "c7#gbw01"
  "MISC16" => "c7#gbw02"
  "MISC17" => "c7#gbw03"
  "MISC18" => "c7#gbw04"
  "MISC22" => "c7#gbw05"
  "MISC23" => "c7#gbw06"
  "MISC24" => "c7#gbw07"
  "MISC26" => "c7#gbw08"
  "MISC21" => "c7#gbw09"
  "MISC25" => "c7#gbw10"
  "MISC20" => "c7#gbw11"
  "MISC27" => "c7#gbw12"
  "MISC29" => "c7#gbw13"
  "MISC30" => "c7#gbw14"
  "MISC28" => "c7#gbw15"
  "MISC32" => "c7#gbw16"
  "MISC31" => "c7#gbw17"
  "MISC35" => "c7#gbw18"
  "MISC33" => "c7#gbw19"
  "MISC34" => "c7#gbw20"
  "MISC36" => "c7#gbw21"
  "MISC37" => "c7#gbw22"
  "MISC38" => "c7#gbw23"
  "MISC39" => "c7#gbw24"
  "MISC40" => "c7#gbw25"
  "MISC42" => "c7#gbw26"
  "MISC43" => "c7#gbw27"
  "MISC41" => "c7#gbw28"
  "MISC44" => "c7#gbw29"
  "MISC45" => "c7#gbw30"
  "MISC1J" => "c7#gbw31"
END

INCLUDE "7c-galileo/lib/gembow_%LANGUAGE%.tpa"

ACTION_PHP_EACH gembow AS gem => bow BEGIN
  ACTION_IF !FILE_EXISTS bow BEGIN
    COPY_EXISTING c7gembow.itm "override/%bow%.itm"
      SAY 0xC EVAL $gembow_names(%bow%)
      SAY 0x54 EVAL $gembow_descs(%bow%)
      PATCH_INCLUDE "7c-galileo/lib/gembow/%bow%.tpa"

    COPY_EXISTING "%gem%.itm" override
      GET_STRREF (LONG_AT 0xC) gem_name
    BUT_ONLY

    COPY "7c-galileo/d/gembow/template.d" "7c-galileo/d/gembow/%bow%.d"
      REPLACE_TEXTUALLY ~gem_item_name_goes_here~ ~%gem%~
      REPLACE_TEXTUALLY ~gembow_item_name_goes_here~ ~%bow%~

    COPY "7c-galileo/lang/%LANGUAGE%/gembow/template.tra" "7c-galileo/lang/%LANGUAGE%/gembow/%bow%.tra"
      REPLACE_TEXTUALLY ~gem_name_goes_here~ ~%gem_name%~
      REPLACE_TEXTUALLY ~gembow_name_goes_here~ ~$gembow_names(%bow%)~

    COMPILE "7c-galileo/d/gembow/%bow%.d" EVAL USING "7c-galileo/lang/%LANGUAGE%/gembow/%bow%.tra" "7c-galileo/lang/%LANGUAGE%/gembow_dialog.tra"
  END
END