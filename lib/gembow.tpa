// -*- weidu-tp2 -*-

// currently, it's a BG1 only file, since misc1j cannot be found in BG2 (misc6z has to be used instead).

COPY_EXISTING "%avaricem%.cre" override
  ADD_CRE_ITEM misc1j #0 #0 #0 identified INV
BUT_ONLY

ACTION_DEFINE_ASSOCIATIVE_ARRAY arrows BEGIN
  // empty on purpose
END

COPY_EXISTING_REGEXP - ~^.*\.itm$~ override
  PATCH_IF SOURCE_SIZE > 0x71 BEGIN
    PATCH_IF SHORT_AT 0x1c == 5 && LONG_AT 0x60 == 0 && NOT ("%SOURCE_RES%" STR_EQ "%tutu_var%AROW01" || "%SOURCE_RES%" STR_EQ "%tutu_var%AROW02") BEGIN
      $arrows("%SOURCE_RES%") = 1
    END
  END ELSE BEGIN
    PRINT ~ERROR: %SOURCE_FILE% is under 0x72 bytes in length: corrupt .itm file!~
  END
BUT_ONLY

ACTION_IF MOD_IS_INSTALLED stratagems.tp2 3010 BEGIN
  $arrows("%tutu_var%AROW02") = 0
END

COPY "7c-galileo/itm/gembow.itm" "override/7cgembow.itm"
  SAY 0x8 #6864
  SAY 0xC @7738
  SAY 0x50 #6866
  SAY 0x54 @7739
  WRITE_BYTE 0x19 (THIS | BIT3)
  PHP_EACH arrows AS arrow => n BEGIN
    PATCH_IF n == 1 BEGIN
      LPF ADD_ITEM_EFFECT
        INT_VAR
          parameter1 = RESOLVE_STR_REF (@7737)
          opcode = 180
          timing = 2
        STR_VAR
          resource = EVAL "%arrow%"
      END
    END
  END

COMPILE "7c-galileo/d/7cgembow.d" USING "7c-galileo/lang/%LANGUAGE%/gembow_dialog.tra"

ACTION_DEFINE_ASSOCIATIVE_ARRAY gembow BEGIN
  "MISC19" => "7c#gbw01"
  "MISC16" => "7c#gbw02"
  "MISC17" => "7c#gbw03"
  "MISC18" => "7c#gbw04"
  "MISC22" => "7c#gbw05"
  "MISC23" => "7c#gbw06"
  "MISC24" => "7c#gbw07"
  "MISC26" => "7c#gbw08"
  "MISC21" => "7c#gbw09"
  "MISC25" => "7c#gbw10"
  "MISC20" => "7c#gbw11"
  "MISC27" => "7c#gbw12"
  "MISC29" => "7c#gbw13"
  "MISC30" => "7c#gbw14"
  "MISC28" => "7c#gbw15"
  "MISC32" => "7c#gbw16"
  "MISC31" => "7c#gbw17"
  "MISC35" => "7c#gbw18"
  "MISC33" => "7c#gbw19"
  "MISC34" => "7c#gbw20"
  "MISC36" => "7c#gbw21"
  "MISC37" => "7c#gbw22"
  "MISC38" => "7c#gbw23"
  "MISC39" => "7c#gbw24"
  "MISC40" => "7c#gbw25"
  "MISC42" => "7c#gbw26"
  "MISC43" => "7c#gbw27"
  "MISC41" => "7c#gbw28"
  "MISC44" => "7c#gbw29"
  "MISC45" => "7c#gbw30"
  "MISC1J" => "7c#gbw31"
END

INCLUDE "7c-galileo/lib/gembow_%LANGUAGE%.tpa"

ACTION_PHP_EACH gembow AS gem => bow BEGIN
  ACTION_IF !FILE_EXISTS bow BEGIN
    COPY_EXISTING 7cgembow.itm "override/%bow%.itm"
      SAY 0xC EVAL $gembow_names(%bow%)
      SAY 0x54 EVAL $gembow_descs(%bow%)
      PATCH_INCLUDE "7c-galileo/lib/gembow/%bow%.tpa"

    COPY_EXISTING "%gem%.itm" override
      GET_STRREF (LONG_AT 0xC) gem_name
    BUT_ONLY

    COPY "7c-galileo/d/gembow/template.d" "7c-galileo/d/gembow/%bow%.d"
      REPLACE_TEXTUALLY ~gem_item_name_goes_here~ ~%gem%~
      REPLACE_TEXTUALLY ~gembow_item_name_goes_here~ ~%bow%~

    COPY "7c-galileo/lang/%LANGUAGE%/gembow/template.tra" "7c-galileo/lang/%LANGUAGE%/gembow/%bow%.tra"
      REPLACE_TEXTUALLY ~gem_name_goes_here~ ~%gem_name%~
      REPLACE_TEXTUALLY ~gembow_name_goes_here~ ~$gembow_names(%bow%)~

    COMPILE "7c-galileo/d/gembow/%bow%.d" EVAL USING "7c-galileo/lang/%LANGUAGE%/gembow/%bow%.tra" "7c-galileo/lang/%LANGUAGE%/gembow_dialog.tra"
  END
END